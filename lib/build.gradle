plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 31
        versionCode 2
        versionName "1.7.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
        flavorDimensions "MARKET"
        direct {
            dimension "MARKET"
            buildConfigField "String", "MARKET", '"direct"'
        }
        auto {
            dimension "MARKET"
            buildConfigField "String", "MARKET", '"direct"'
        }
        cafeBazaar {
            dimension "MARKET"
            buildConfigField "String", "MARKET", '"cafebazaar"'
        }
        myket {
            dimension "MARKET"
            buildConfigField "String", "MARKET", '"myket"'
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'com.github.vahid-m:BillingAidl:v1.0'
}

afterEvaluate {
    android.libraryVariants.each { variant ->
        // Only consider release
        if (variant.buildType.name != "release") { return }
        def libraryGroupId = 'com.cluby'
        def libraryArtifactId = 'pwa-wrapper'
        def libraryVersion = '1.7.1'

        publishing.publications.create(variant.name, MavenPublication) {
            from components.findByName(variant.name)
            groupId libraryGroupId
            artifactId "${libraryArtifactId}-${variant.flavorName}"
            version libraryVersion
        }
    }
}